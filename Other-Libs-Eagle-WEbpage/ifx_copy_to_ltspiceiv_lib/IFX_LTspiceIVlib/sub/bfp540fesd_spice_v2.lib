***************************************************************												
*	Infineon	Technologies	AG									
*	GUMMEL-POON	MODEL	IN	SPICE	2G6	SYNTAX				
*	VALID	UP	TO	8	GHZ							
*	>>>	BFP540FESD	<<<								
*	(C)	2012	Infineon	Technologies	AG							
*	Version 2	January	2012								
***************************************************************
* - Please use the global SPICE parameter TEMP to set the junction
*   temperature of this device in your circuit to get correct DC 
*   simulation results. 
* - TEMP is calculated by TEMP=TA+P*(RthJS+RthSA). The junction 
*   temperature TEMP is the sum of the ambient temperature TA and 
*   the temperature increment caused by the dissipated power 
*   P=VCE*IC (IC collector current, VCE collector-emitter voltage). 
* - RthJS is the thermal resistance between the junction and the 
*   soldering point. RthJS for this device is 280 K/W. RthSA is the 
*   thermal resistance of the PCB, from the soldering point to the 
*   ambient. For determination of RthSA please refer to Infineon's 
*   Application Note "Thermal Resistance Calculation" AN077.
* - The model has been verified in the junction temperature range
*   0°C to +125°C.
* - TNOM=25 °C is the nominal ambient temperature.
*   Please do not change this value.
****************************************************************				
*BFP540FESD C B E1 E2 
.SUBCKT BFP540FESD 1 2 3 4

CBEPAR 22 33 3.758E-013
CBCPAR 22 11 1.32003E-013
CCEPAR 11 33 4.41012E-014
LB    22 20 5.25538E-010
LE   33 30 1.7602E-010
LC   11 10  4.1569E-010
CBEPCK 20 30  3.3795E-014
CBCPCK 20 10  1E-015
CCEPCK 10 30  3.04416E-014
LBX    20 2 1.69782E-010
LEX   30 35 7.67852E-011
LCX   10 1  1.63982E-010

RS 55 33 91.8342
RE1 35 3 1E-03
RE2 35 4 1E-03

Diode_sub 55 11 M_Diode_sub
Q1 11 22 33 55 M_BFP540FESD

.MODEL M_Diode_sub D(
+	IS	=	5.566E-015				
+	N	=	1			
+	RS	=	400			
+	CJO	=	9.14809E-014)			

.MODEL 	M_BFP540FESD  	NPN(										
+	IS	=	6.79E-017									
+	BF	=	205.5									
+	NF	=	1									
+	VAF	=	46.19									
+	IKF	=	0.5278									
+	ISE	=	3.946E-014									
+	NE	=	2									
+	BR	=	4.66		
+	NR	=	1		
+	VAR	=	1.843	
+	IKR	=	0.1924		
+	ISC	=	3.85E-015		
+	NC	=	1.5		
+	RB	=	17.6828		
+	IRB	=	0		
+	RBM	=	2.234	
+	RE	=	0.1699		
+	RC	=	2.484	
+	XTB	=	-1.003		
+	EG	=	1.11		
+	XTI	=	2.786		
+	CJE	=	3.354E-013		
+	VJE	=	1.453		
+	MJE	=	0.8833		
+	TF	=	3.77825E-012		
+	XTF	=	5.037		
+	VTF	=	6.92041		
+	ITF	=	0.719327		
+	PTF	=	0.1		
+	CJC	=	6.349E-014		
+	VJC	=	0.8371		
+	MJC	=	0.9968		
+	XCJC	=	0.7031		
+	TR	=	4.163E-009
+	CJS	=	4.19E-013		
+	MJS	=	0.164			
+	VJS	=	0.1689 		
+	FC	=	0.6027		
+	KF	=	2		
+	AF	=	8.9E-011)		
***************************************************************					
				
.ENDS BFP540FESD

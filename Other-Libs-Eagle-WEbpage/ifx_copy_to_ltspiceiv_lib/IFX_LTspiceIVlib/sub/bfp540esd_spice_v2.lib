***************************************************************												
*	Infineon	Technologies	AG									
*	GUMMEL-POON	MODEL	IN	SPICE	2G6	SYNTAX				
*	VALID	UP	TO	10	GHZ							
*	>>>	BFP540ESD <<<								
*	(C)	2011	Infineon	Technologies	AG							
*	Version 2.0		November	2011								
***************************************************************	
* - Please use the global SPICE parameter TEMP to set the junction
*   temperature of this device in your circuit to get correct DC 
*   simulation results. 
* - TEMP is calculated by TEMP=TA+P*(RthJS+RthSA). The junction 
*   temperature TEMP is the sum of the ambient temperature TA and 
*   the temperature increment caused by the dissipated power 
*   P=VCE*IC (IC collector current, VCE collector-emitter voltage). 
* - RthJS is the thermal resistance between the junction and the 
*   soldering point. RthJS for this device is 290 K/W. RthSA is the 
*   thermal resistance of the PCB, from the soldering point to the 
*   ambient. For determination of RthSA please refer to Infineon's 
*   Application Note "Thermal Resistance Calculation" AN077.
* - The model has been verified in the junction temperature range
*   0°C to +125°C.
* - TNOM=25 °C is the nominal ambient temperature.
*   Please do not change this value.
****************************************************************				
*BFP540ESD C B E1 E2 
.SUBCKT BFP540ESD 1 2 3 4 

CBEPAR 22 33 3.19737E-013
CBCPAR 22 11 1.45578E-013
CCEPAR 11 33 3.31838E-014
LB    22 20 7.1E-010
LE   33 30 1.73226E-010
LC   11 10  5.45033E-010
CBEPCK 20 30  9.99916E-014
CBCPCK 20 10  1.00897E-015
CCEPCK 10 30  9.96469E-014
LBX    20 2 3.46463E-010
LEX   30 35 6.02121E-011
LCX   10 1  3.89864E-010

RS 55 30 82.8307
RE1 35 3 1E-03
RE2 35 4 1E-03

Diode_sub 55 11 M_Diode_sub

Q1 11 22 33 55 M_BFP540ESD

.MODEL M_Diode_sub D(
+	IS	=	5.132E-016				
+	N	=	1			
+	RS	=	400			
+	CJO	=	4E-014)			

.MODEL 	M_BFP540ESD  NPN(										
+	IS	=	6.79E-017									
+	BF	=	205.5									
+	NF	=	1									
+	VAF	=	46.19									
+	IKF	=	0.5278									
+	ISE	=	3.946E-014									
+	NE	=	2									
+	BR	=	4.66		
+	NR	=	1		
+	VAR	=	1.843	
+	IKR	=	0.1924		
+	ISC	=	3.85E-015		
+	NC	=	1.5		
+	RB	=	13.9992		
+	IRB	=	0		
+	RBM	=	2.234	
+	RE	=	0.1699		
+	RC	=	2.484	
+	XTB	=	-1.003		
+	EG	=	1.11		
+	XTI	=	2.786		
+	CJE	=	3.354E-013		
+	VJE	=	1.453		
+	MJE	=	0.8833		
+	TF	=	3.624E-012		
+	XTF	=	5.037		
+	VTF	=	3.248		
+	ITF	=	0.7287		
+	PTF	=	0.1		
+	CJC	=	6.349E-014		
+	VJC	=	0.8371		
+	MJC	=	0.9968		
+	XCJC	=	0.7031		
+	TR	=	4.163E-009
+	CJS	=	4.19E-013		
+	MJS	=	0.164			
+	VJS	=	0.1689 		
+	FC	=	0.6027		
+	KF	=	2.01		
+	AF	=	18.9E-011)			
***************************************************************					
				
.ENDS BFP540ESD

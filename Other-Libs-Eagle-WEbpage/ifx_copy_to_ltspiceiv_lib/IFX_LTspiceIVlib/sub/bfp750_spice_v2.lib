***************************************************************												
*	Infineon	Technologies	AG									
*	GUMMEL-POON	MODEL	IN	SPICE	2G6	SYNTAX				
*	VALID	UP	TO	10	GHZ							
*	>>>	BFP750	<<<								
*	(C)	2010	Infineon	Technologies	AG							
*	Version 2.0	September	2010									
***************************************************************	
* - Please use the global SPICE parameter TEMP to set the junction
*   temperature of this device in your circuit to get correct DC 
*   simulation results. 
* - TEMP is calculated by TEMP=TA+P*(RthJS+RthSA). The junction 
*   temperature TEMP is the sum of the ambient temperature TA and 
*   the increment of temperature caused by the dissipated power 
*   P=VCE*IC (IC collector current, VCE collector-emitter voltage). 
* - RthJS is the thermal resistance between the junction and the 
*   soldering point. RthJS for this device is 360 K/W. RthSA is the 
*   thermal resistance of the PCB, from the soldering point to the 
*   ambient. For determination of RthSA please refer to Infineon's 
*   Application Note 'Thermal Resistance Calculation' AN077.
* - The model has been verified in the junction temperature range
*   -25°C to +125°C.
* - TNOM=25 °C is the nominal ambient temperature.
*   Please do not change this value.
****************************************************************						
*BFP750 C B E1 E2
.SUBCKT BFP750 1 2 3 4


CBEPAR 22 33 2.718E-013
CBCPAR 22 11 1.398E-013
CCEPAR 11 33 2.07221E-015
LB    22 20 9.917E-010
LC   11 10  7.02587E-011
CBEPCK 20 30  1.819E-014
CBCPCK 20 10  3.736E-018
CCEPCK 10 30  3.99247E-013
LBX    20 2 6.738E-015
LEX   30 35 9.035E-011
LCX   10 1  8.402E-010
RSUB 30 5 0.19
RSUBNPN 55 5 578
Rpsinker 5 33 0.16
RE1 35 3 1E-03
RE2 35 4 1E-03

Q1 11 22 33 55 M_BFP750



.MODEL 	M_BFP750	NPN(										
+	IS	=	3.079E-015									
+	BF	=	624.106									
+	NF	=	1.011									
+	VAF	=	150.2									
+	IKF	=	0.245321									
+	ISE	=	1.31E-014									
+	NE	=	1.666									
+	BR	=	138.7		
+	NR	=	0.98		
+	VAR	=	1.727	
+	IKR	=	0.1924		
+	ISC	=	3.85E-015		
+	NC	=	1.5		
+	RB	=	2.891		
+	IRB	=	0.000889		
+	RBM	=	1.504	
+	RE	=	0.2121		
+	RC	=	4.472	
+	XTB	=	-2.066		
+	EG	=	1.034		
+	XTI	=	0.808		
+	CJE	=	6.905E-013		
+	VJE	=	1.274		
+	MJE	=	0.3898		
+	TF	=	1.9094E-012		
+	XTF	=	5.245		
+	VTF	=	1.3		
+	ITF	=	1.6		
+	PTF	=	0.5287		
+	CJC	=	2.491E-013		
+	VJC	=	0.5152		
+	MJC	=	0.4138		
+	XCJC	=	0.6466		
+	TR	=	5.76636E-008
+	CJS	=	4.70074E-013		
+	MJS	=	0.4784
+   VJS =   0.4645 		
+	FC	=	0.8156		
+	KF	=	2.0E-011		
+	AF	=	1.6)
***************************************************************					
					

.ENDS BFP750

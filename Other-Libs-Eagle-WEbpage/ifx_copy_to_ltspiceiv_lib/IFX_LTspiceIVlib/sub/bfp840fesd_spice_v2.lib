***************************************************************												
*	Infineon	Technologies	AG									
*	GUMMEL-POON	MODEL	IN	SPICE	2G6	SYNTAX				
*	VALID	UP	TO	12	GHZ							
*	>>>	BFP840FESD	<<<								
*	(C)	2012	Infineon	Technologies	AG							
*	Version 2.0	March	2012							
***************************************************************
* - Please use the global SPICE parameter TEMP to set the junction
*   temperature of this device in your circuit to get correct DC 
*   simulation results. 
* - TEMP is calculated by TEMP=TA+P*(RthJS+RthSA). The junction 
*   temperature TEMP is the sum of the ambient temperature TA and 
*   the increment of temperature caused by the dissipated power 
*   P=VCE*IC (IC collector current, VCE collector-emitter voltage). 
* - RthJS is the thermal resistance between the junction and the 
*   soldering point. RthJS for this device is 541 K/W. RthSA is the 
*   thermal resistance of the PCB, from the soldering point to the 
*   ambient. For determination of RthSA please refer to Infineon's 
*   Application Note "Thermal Resistance Calculation" AN077.
* - The model has been verified in the junction temperature range
*   -25°C to +125°C.
* - TNOM=25 °C is the nominal ambient temperature.
*   Please do not change this value.
****************************************************************			
*BFP840FESD C B E1 E2
.SUBCKT BFP840FESD 1 2 3 4

RE1 35 3 1E-03
RE2 35 4 1E-03

CBEPAR 22 33 1.5E-013
CBCPAR 22 11 3E-014
CCEPAR 11 33 1.5E-013
LB    222 20 4.3E-010
LE    333 30 1.5E-010
LC    111 10 4.3E-010
RBs   222 22 1.728
REs   333 33 2.5
RCs   111 11 5
K_BC  LB LC 0.001
K_EB  LE LB 0.1
K_EC  LE LC 0.2

CBEPCK 20 30  1.2E-014
CBCPCK 20 10  1.134E-014
CCEPCK 10 30  1.2E-014
LBX    20 2 5E-011
LEX   30 35 4.851E-011
LCX   10 1  2.329E-010

R_CS_npn 44 5 0.1

D1 33 25 M_D1  
D2 5 25  M_D2

R_NBL_fbd 22 25 1
RPS 33 5 0.09
RSUB 30 5 0.001

D3 5 15 M_D3
D4 23 33 M_D4
D5 23 15 M_D5

R_NBL_e11g 15 11 1

.MODEL M_D1 D(
+ IS=3E-015	
+ N=0.9999
+ RS=2.846
+ M=0.5499
+ CJ0=1E-014
+ VJ=0.7)

.MODEL M_D2 D(
+ IS= 3E-015	
+ N=  0.9999
+ RS= 1208
+ CJ0=5E-015
+ M=  0.5499
+ VJ= 0.7)

.MODEL M_D3 D(
+ IS=1E-015	
+ N=1.1
+ RS=4969
+ CJ0 =1E-014
+ M =0.4997
+ VJ=0.7001)

.MODEL M_D4 D(
+ IS=3.5E-015	
+ N=0.9999
+ RS=0.2
+ CJ0 =1E-014
+ M =0.4999
+ VJ=0.7)

.MODEL M_D5 D(
+ IS=3.5E-015	
+ N=1.02
+ RS=4.7
+ CJ0 =1.2E-014
+ M =0.4999
+ VJ=0.7)


Q1 11 22 33 44 M_BFP840FESD
.MODEL 	M_BFP840FESD	NPN(	
+	LEVEL	=	1									
+	IS	=	2.009E-016									
+	BF	=	946.1									
+	NF	=	1									
+	VAF	=	300									
+	IKF	=	0.1993									
+	ISE	=	6.26E-015									
+	NE	=	2									
+	BR	=	108		
+	NR	=	1		
+	VAR	=	1.288	
+	IKR	=	0.0007328		
+	ISC	=	8.126E-016		
+	NC	=	2		
+	RB	=	17		
+	IRB	=	0		
+	RBM	=	1	
+	RE	=	0.05		
+	RC	=	4.145	
+	XTB	=	-2.276		
+	EG	=	1.11		
+	XTI	=	0		
+	CJE	=	1.3E-013		
+	VJE	=	0.6		
+	MJE	=	0.5		
+	TF	=	7.494E-013		
+	XTF	=	17.49		
+	VTF	=	0.5295		
+	ITF	=	1.025		
+	PTF	=	4.667		
+	CJC	=	1.1E-014		
+	VJC	=	0.67		
+	MJC	=	0.88		
+	XCJC	=	0.4894		
+	TR	=	1.793E-009
+	CJS	=	3E-013		
+	MJS	=	0.5
+   VJS =   0.7 		
+	FC	=	0.352		
+	KF	=	6.4E-012		
+	AF	=	1.44)
***************************************************************

.ENDS BFP840FESD

